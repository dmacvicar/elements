###############################################################################
#  Copyright (c) 2016-2020 Joel de Guzman
#
#  Distributed under the MIT License (https://opensource.org/licenses/MIT)
###############################################################################
cmake_minimum_required(VERSION 3.9.6...3.15.0)

project(elements LANGUAGES C CXX)

set(DEFAULT_BUILD_TYPE "Release")
if(NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
    message(STATUS "Setting build type to '${DEFAULT_BUILD_TYPE}' as none was specified.")
    set(CMAKE_BUILD_TYPE "${DEFAULT_BUILD_TYPE}" CACHE
        STRING "Choose the type of build." FORCE)
    # Set the possible values of build type for cmake-gui
    set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS
        "Debug" "Release" "MinSizeRel" "RelWithDebInfo")
endif()

include(CheckIPOSupported)
check_ipo_supported(RESULT IPO_SUPPORTED)
if(IPO_SUPPORTED)
    message(STATUS "Link-time optimization supported. Will be enabled in Release build type")
endif()

include(FetchContent)
if (NOT DEFINED cycfi_json_SOURCE_DIR)
  FetchContent_Declare(cycfi_json
    GIT_REPOSITORY https://github.com/cycfi/json.git
  )
  FetchContent_MakeAvailable(cycfi_json)
else()
  add_subdirectory(${cycfi_json_SOURCE_DIR} ${CMAKE_CURRENT_BINARY_DIR}/json)
endif()

if (NOT DEFINED cycfi_infra_SOURCE_DIR)
  FetchContent_Declare(cycfi_infra
    GIT_REPOSITORY https://github.com/cycfi/infra.git
  )
  FetchContent_MakeAvailable(cycfi_infra)
else()
  add_subdirectory(${cycfi_infra_SOURCE_DIR} ${CMAKE_CURRENT_BINARY_DIR}/infra)
endif()

add_subdirectory(lib)

option(ELEMENTS_BUILD_EXAMPLES "build Elements library examples" ON)

if (ELEMENTS_BUILD_EXAMPLES)
   add_subdirectory(examples)
endif()
